plugins {
	id 'java'
	id 'jacoco'
	id 'org.springframework.boot' version '3.1.3'
	id 'io.spring.dependency-management' version '1.1.3'
}

group = 'dev.brunocesar.imovelsimplificado'

jar {
	archiveFileName = 'advert'
}


java {
	sourceCompatibility = '17'
}

repositories {
	mavenCentral()
}

ext {
	set("awsSdkVersion", "2.20.153")
	set("lombokVersion", "1.18.24")
	set("h2Version", "2.2.224")
	set("mySqlConectorVersion", "8.0.33")
	set("gsonVersion", "2.10.1")
	set("springDocVersion", "2.2.0")
	set("mockWebServerVersion", "4.11.0")
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-validation'

	implementation "software.amazon.awssdk:s3:${awsSdkVersion}"
	implementation "software.amazon.awssdk:sqs:${awsSdkVersion}"
	implementation "software.amazon.awssdk:sts:${awsSdkVersion}"

	implementation "org.projectlombok:lombok:${lombokVersion}"
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	implementation "com.google.code.gson:gson:${gsonVersion}"

	implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:${springDocVersion}"

	runtimeOnly "mysql:mysql-connector-java:${mySqlConectorVersion}"

	// Tests
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation "com.h2database:h2:${h2Version}"
	testImplementation "com.squareup.okhttp3:mockwebserver:${mockWebServerVersion}"
}

tasks.named('test') {
	useJUnitPlatform()
	dependsOn 'cleanTest'
	finalizedBy jacocoTestReport
}

jacoco {
	toolVersion = "0.8.9"
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.required = false
		csv.required = false
		html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
	}
	afterEvaluate {
		getClassDirectories().setFrom(classDirectories.files.collect({
			fileTree(dir: it, exclude: [
					'dev/brunocesar/imovelsimplificado/advert/config/**',
					'dev/brunocesar/imovelsimplificado/advert/domains/**',
					'dev/brunocesar/imovelsimplificado/advert/controllers/requests/**',
					'dev/brunocesar/imovelsimplificado/advert/controllers/responses/**',
					'dev/brunocesar/imovelsimplificado/advert/controllers/handler/**',
					'dev/brunocesar/imovelsimplificado/advert/controllers/exceptions/**',
					'dev/brunocesar/imovelsimplificado/advert/gateway/response/**',
					'dev/brunocesar/imovelsimplificado/advert/AdvertApplication.class'
			])
		}))
	}
}

